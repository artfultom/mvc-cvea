apply plugin: "java-library"
apply plugin: "maven-publish"
apply plugin: "signing"

group = "io.github.artfultom"
version = "0.0.7" + (project.hasProperty("release") ? "" : "-SNAPSHOT")

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    api "com.squareup:javapoet:1.13.0"

    api "com.fasterxml.jackson.core:jackson-databind:2.13.3"
    api "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.3"
    api "org.slf4j:slf4j-api:1.7.36"

    testImplementation "junit:junit:4.13.2"
    testImplementation "org.slf4j:slf4j-simple:1.7.36"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = "vecenta"
            groupId = project.group
            version = project.version
            from(components["java"])
            pom {
                packaging = "jar"
                name.set("Vecenta")
                url.set("https://github.com/artfultom/vecenta")
                description.set("RPC Framework")

                licenses {
                    license {
                        name.set("MIT License")
                        url.set("https://raw.githubusercontent.com/artfultom/vecenta/master/LICENSE")
                    }
                }

                scm {
                    connection.set("scm:https://github.com/artfultom/vecenta.git")
                    developerConnection.set("scm:git@github.com:artfultom/vecenta.git")
                    url.set("https://github.com/artfultom/vecenta")
                }

                developers {
                    developer {
                        id.set("artfultom")
                        name.set("Andrey Vasilyev")
                        email.set("artfultom@protonmail.com")
                    }
                }
            }
        }
    }
    repositories {
        if (project.hasProperty("release")) {
            maven {
                url = "https://s01.oss.sonatype.org/content/repositories/releases/"
                credentials {
                    username = System.getenv("USER")
                    password = System.getenv("PASS")
                }
            }
        } else {
            maven {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                credentials {
                    username = System.getenv("USER")
                    password = System.getenv("PASS")
                }
            }
        }
    }
}

signing {
    sign publishing.publications.maven
    def signingKey = System.getenv("OSS_KEY")
    def signingPassword = System.getenv("OSS_PASS")
    useInMemoryPgpKeys(signingKey, signingPassword)
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}